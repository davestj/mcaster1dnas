#!Nsis Installer Command Script
#
# This is an NSIS Installer Command Script generated automatically
# by the Fedora nsiswrapper program.  For more information see:
#
#   http://fedoraproject.org/wiki/MinGW
#
# To build an installer from the script you would normally do:
#
#   makensis this_script
#
# which will generate the output file 'installer.exe' which is a Windows
# installer containing your program.

!include WinVer.nsh

Name "Mcaster1 KH"
OutFile "mcaster1-@PACKAGE_VERSION@_win64_setup.exe"

InstallDir "$ProgramFiles64\Mcaster1 KH"
InstallDirRegKey HKLM SOFTWARE\Mcaster1 "Install_Dir"

LoadLanguageFile "${NSISDIR}\Contrib\Language files\English.nlf"

ShowInstDetails hide
ShowUninstDetails hide

# Uncomment this to enable BZip2 compression, which results in
# slightly smaller files but uses more memory at install time.
SetCompressor bzip2

XPStyle on

;Version Information

VIProductVersion "2.4.0.22"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "Mcaster1 KH"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductVersion" "2.4.0"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "Karl Heyes @2000-2023"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "Mcaster1 KH streaming media server installer"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileVersion" "@PACKAGE_VERSION@"

Page components
Page directory
Page instfiles

ComponentText "Select which optional components you want to install."

DirText "Please select the installation folder."

Function un.onInit
    MessageBox MB_YESNO "Uninstall mcaster1 KH from $INSTDIR ?" IDYES NoAbort
      Abort ; causes uninstaller to quit.
    NoAbort:
FunctionEnd

Section "Mcaster1 Installation"
  SectionIn RO

  SetOutPath "$INSTDIR"
  File /r "x64/*.dll"

  ${If} ${AtMostWin7}
  FILE /r "x64/extra/*.dll"
  ${EndIf}

  WriteUninstaller "$INSTDIR\Uninstall.exe"

  File "x64/mcaster1.exe"
  SetOverwrite off
  File "x64/mcaster1.xml"
  SetOverwrite on

  SetOutPath "$INSTDIR\logs"
  File /r "x64/logs/"

  SetOutPath "$INSTDIR\admin"
  File /r "x64/admin/"

  SetOutPath "$INSTDIR\doc"
  File /r "x64/doc/"

  SetOutPath "$INSTDIR\web"
  File /r "x64/web/"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Mcaster1 KH" \
                 "DisplayName" "Mcaster1 KH"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Mcaster1 KH" \
                 "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
SectionEnd

Section "Install Service"
  SetOutPath "$INSTDIR"
  ExecWait '"$INSTDIR\mcaster1.exe" "install" "$INSTDIR"'
SectionEnd

Section "Start Menu Shortcuts"
  CreateDirectory "$SMPROGRAMS\Mcaster1 KH"
  SetOutPath "$INSTDIR"
  CreateShortCut "$SMPROGRAMS\Mcaster1 KH\Edit Configuration.lnk" "notepad.exe" "$INSTDIR\mcaster1.xml"
  CreateShortCut "$SMPROGRAMS\Mcaster1 KH\Uninstall Mcaster1.lnk" "$INSTDIR\Uninstall.exe" "" "$INSTDIR\Uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\Mcaster1 KH\Run Mcaster1.lnk" "$INSTDIR\mcaster1.exe" '-c "$INSTDIR\mcaster1.xml"'
SectionEnd

Section "Desktop Icons"
  SetOutPath "$INSTDIR"
  CreateShortCut "$DESKTOP\Mcaster1.lnk" "$INSTDIR\mcaster1.exe" '-c "$INSTDIR\mcaster1.xml"'
SectionEnd

Section "Uninstall"
  ExecWait '"$INSTDIR\mcaster1.exe" "remove"'
  Delete $INSTDIR\Uninstall.exe
  Delete /rebootok "$DESKTOP\Mcaster1.lnk"
  Delete /rebootok "$SMPROGRAMS\Mcaster1 KH\Run Mcaster1.lnk"
  Delete /rebootok "$SMPROGRAMS\Mcaster1 KH\Uninstall Mcaster1.lnk"
  Delete /rebootok "$SMPROGRAMS\Mcaster1 KH\Edit Configuration.lnk"
  RMDir "$SMPROGRAMS\Mcaster1 KH"

  RMDir /r "$INSTDIR\web"
  RMDir /r "$INSTDIR\doc"
  RMDir /r "$INSTDIR\admin"

  Delete /rebootok "$INSTDIR\mcaster1.exe"

  Delete /rebootok "$INSTDIR\*.dll"

  RMDir "$INSTDIR"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Mcaster1 KH"
SectionEnd
